// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // negated x and y
    Not16(in=x, out=notx);
    Not16(in=y, out=noty);

    // mux's for zx and nx - if should set to 0 and/or negate
    Mux4Way16(a=x, b=false, c=notx, d=true, sel[0]=zx, sel[1]=nx, out=finalx);
    Mux4Way16(a=y, b=false, c=noty, d=true, sel[0]=zy, sel[1]=ny, out=finaly);

    // x + y or x & y
    Add16(a=finalx, b=finaly, out=xyadded);
    And16(a=finalx, b=finaly, out=xandy);
    Mux16(a=xandy, b=xyadded, sel=f, out=fresult);

    // if no then out = !out
    Not16(in=fresult, out=notfresult);
    Mux16(a=fresult, b=notfresult, sel=no, out=out, out[0..7]=out7, out[8..15]=out15, out[15]=negative);

    // zr - check if all are 0
    Or8Way(in=out7, out=any7);
    Or8Way(in=out15, out=any15);
    Or(a=any7, b=any15, out=anyall);
    Xor(a=anyall, b=true, out=zr);

    // ng - check if negative
    And(a=negative, b=true, out=ng);

    



}